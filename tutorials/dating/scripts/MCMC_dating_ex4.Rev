########################################################
#
# RevBayes Example: Molecular dating
# 
# This file: Runs the full MCMC for exercise 4
#
# authors: Rachel C. M. Warnock, Sebastian Hoehna
#
########################################################

##################################################################################
#
# Exercise 4: Estimating speciation times using the fossilized birth-death process
#
##################################################################################

#######################
# Reading in the Data #
#######################

# Create the full list of taxa (including all fossils and extant species) #
taxa <- readTaxonData("data/bears_taxa.tsv")

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
cytb <- readDiscreteCharacterData("data/bears_cytb.nex")

# Add the missing taxa to each data partition #
cytb.addMissingTaxa( taxa )

## helpers
n_taxa <- cytb.size()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

# Load the model files

source("scripts/tree_FBD.Rev") # BD tree prior + node calibrations

source("scripts/clock_global.Rev") # Global clock model
#source("scripts/clock_relaxed_lognormal.Rev") # Global clock model

source("scripts/sub_GTRG.Rev") # Molecular substitution model (GTR+G)

########
# MCMC #
########

# initialize the model object #
mymodel = model(sf)	

# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/bears_FBD.log", printgen=1) )

# 2. the tree #
monitors.append( mnFile(filename="output/bears_FBD_all.trees", printgen=1, timetree) )
monitors.append( mnFile(filename="output/bears_FBD_extant.trees", printgen=1, pruned_tree) )

# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=100, diversification) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=20000, tuningInterval=200)

########################
# Summarizing the tree #
########################

# Read the trace file #
trace_extant = readTreeTrace("output/bears_FBD_extant.trees")
trace = readTreeTrace("output/bears_FBD_all.trees")

# Maximum clade credibility tree #
mccTree(trace_extant, file="output/bears_FBD_extant.mcc.tre" )
mccTree(trace, file="output/bears_FBD_all.mcc.tre" )

# Quit RevBayes #
q()
